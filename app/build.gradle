apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

Properties props = new Properties()
File propsFile = file('app.properties')
if (propsFile.exists()) {
    props.load(propsFile.newDataInputStream())
} else {
    throw new GradleException("Missing app.properties, check the app.properties.sample file.")
}

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.psimao.rtcplayground"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "USER_ALIAS", "\"${props.getProperty("userAlias")}\""
        buildConfigField "String", "SIGNALLING_SERVER_URL", "\"${props.getProperty("signallingServerUrl")}\""

        resValue "string", "maps_key", "\"${props.getProperty("mapsKey")}\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'org.webrtc:google-webrtc:1.0.23546'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation "com.github.nkzawa:socket.io-client:0.3.0"
    implementation "com.google.android.gms:play-services-location:15.0.1"
    implementation 'com.google.android.gms:play-services-maps:15.0.1'
    implementation 'com.airbnb.android:lottie:2.5.5'
    implementation "org.koin:koin-core:0.9.3"
    implementation "org.koin:koin-android:0.9.3"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:0.23.4"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

kotlin {
    experimental {
        coroutines "enable"
    }
}
